;; augmented BNF grammar for the XAS Data Interchange format
;; see RFC 5234, http://tools.ietf.org/html/rfc5234, for grammar syntax

;; start rule
XDI            = VERSION [FIELDS] [COMMENTS] [LABELS] DATA

;; core rules
OCTET          = %x00-FF             ; 8 bits of data
UPALPHA        = %x41-5A             ; upper case letters A - Z
LOALPHA        = %x61-7A             ; lower case letters a - z
CHAR           = %x01-7F             ; any 7-bit US-ASCII character, excluding NUL
VCHAR          = %x21-7E             ; visible (printing) characters, 7-bit (US-ASCII)
ALPHA          = UPALPHA / LOALPHA   ; US-ASCII letters
DIGIT          = %x30-39             ; digits 0 - 9
CTL            = %x00-1F / %x7F      ; control characters (octets 0 - 31) and DEL (127)
CR             = %x0D                ; carriage return
LF             = %x0A                ; line feed
CRLF           = CR LF               ; MS newline = carriage return line feed
SP             = %x20                ; space
HT             = %x09                ; horizontal tab
WS             = SP  /  HT           ; white space
EOL            = CR  /  LF  /  CRLF  ; cross-platform end-of-line 

;; Basic Constructs
SIGN           = "+"  /  "-"
EXPONENT       = ("e"  /  "E"  /  "d"  /  "D")  [SIGN]  *DIGIT
NUMBER         = *DIGIT  ["."  1*DIGIT]  [EXPONENT]
INF            = ("i"  /  "I")  ("n"  /  "N")  ("f"  /  "F")
NAN            = ("n"  /  "N")  ("a"  /  "A")  ("n"  /  "N")
FLOAT          = [SIGN]  (NUMBER  /  INF  / NAN )
DATETIME       = 4DIGIT  "-"  2DIGIT  "-"  2DIGIT "T" 2DIGIT  ":"  2DIGIT  ":"  2DIGIT

TEXT           = %09 / %x20-FF        ; any OCTET except CTLs, including WS
COMM           = COMM / ";"
PROPERWORD     = ALPHA  *(ALPHA  /  DIGIT  /  "_" / "-")
WORD           = 1*(ALPHA  /  DIGIT  /  "_")
MATH           = ["ln"] *("-"  /  "+"  /  "*"  /  "$"  /  "/"  /  "("  /  ")"  DIGIT)

FIELD-END      = COMM  2*"/"  EOL
HEADER-END     = COMM  2*"-"  EOL

;; Version Information
XDI-VERSION    = "XDI/"  *DIGIT  ". " *DIGIT
APPLICATIONS   = VCHAR
VERSION        = COMM  XDI-VERSION  *APPLICATIONS  EOL

;; Defined Fields
ABSCISSA           = COMM  "Abscissa"           ": "  MATH
BEAMLINE           = COMM  "Beamline"           ": "  *WORD
DSPACING           = COMM  "D_spacing"          ": "  FLOAT
EDGEENERGY         = COMM  "Edge_energy"        ": "  FLOAT
ENDTIME            = COMM  "End_time"           ": "  DATETIME
MUFLUOR            = COMM  "Mu_fluorescence"    ": "  MATH
MUREF              = COMM  "Mu_reference"       ": "  MATH
MUTRANS            = COMM  "Mu_transmission"    ": "  MATH
STARTTIME          = COMM  "Start_time"         ": "  DATETIME

DEFINEDFIELDS      = *( (   ABSCISSA     / BEAMLINE   
                          / DSPACING     / EDGEENERGY
                          / STARTTIME    / ENDTIME
                          / MUFLUOR      / MUREF       / MUTRANS
                          / EXT_FIELD    / FIELD_LINE
                         ) EOL ) FIELD-END

;; Extension Fields
FIELD-LINE     = DEFINEDFIELDS
EXT-FIELD-NAME = PROPERWORD  *("-"  PROPERWORD)

FIELD-LINE     = COMM  FIELD-NAME      ": "  *WORD   EOL
EXT-FIELD      = COMM  EXT-FIELD-NAME  ": "  *VCHAR  EOL

;; All Fields
FIELDS         =  (FIELD-LINE / EXT-FIELD)(s) FIELD_END


;; User Comments
COMMENT-LINE   = COMM  *VCHAR  EOL
COMMENTS       = *COMMENT-LINE  HEADER-END

;; Column Labels
LABEL          = WORD
LABELS         = COMM 1*LABEL  EOL

;; Data Section
DATA-LINE      = *FLOAT EOL
DATA           = *DATA-LINE
