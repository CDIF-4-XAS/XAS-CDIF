;; augmented BNF grammar for the XAS Data Interchange format
;; see RFC 5234, http://tools.ietf.org/html/rfc5234, for grammar syntax

;; start rule
XDI            = VERSION FIELDS [COMMENTS] [LABELS] DATA

;; core rules
OCTET          = %x00-FF             ; 8 bits of data
UPALPHA        = %x41-5A             ; upper case letters A - Z
LOALPHA        = %x61-7A             ; lower case letters a - z
CHAR           = %x01-7F             ; any 7-bit US-ASCII character, excluding NUL
VCHAR          = %x21-7E             ; visible (printing) characters, 7-bit (US-ASCII)
ALPHA          = UPALPHA / LOALPHA   ; US-ASCII letters
DIGIT          = %x30-39             ; digits 0 - 9
CTL            = %x00-1F / %x7F      ; control characters (octets 0 - 31) and DEL (127)
CR             = %x0D                ; carriage return
LF             = %x0A                ; line feed
CRLF           = CR LF               ; MS newline = carriage return line feed
SP             = %x20                ; space
HT             = %x09                ; horizontal tab
WS             = SP  /  HT           ; white space
EOL            = CR  /  LF  /  CRLF  ; cross-platform end-of-line 

;; Basic Constructs
SIGN           = "+"  /  "-"
EXPONENT       = ("e"  /  "E"  /  "d"  /  "D")  [SIGN]  *DIGIT
NUMBER         = *DIGIT  ["."  *DIGIT]  [EXPONENT]
INF            = ("i"  /  "I")  ("n"  /  "N")  ("f"  /  "F")
NAN            = ("n"  /  "N")  ("a"  /  "A")  ("n"  /  "N")
FLOAT          = [SIGN]  (NUMBER  /  INF  / NAN )
INTEGER        = 1*DIGIT
DATETIME       = 4DIGIT  "-"  2DIGIT  "-"  2DIGIT "T" 2DIGIT  ":"  2DIGIT  ":"  2DIGIT
      
TEXT           = %09 / %x20-FF        ; any OCTET except CTLs, including WS
COMM           = "#" / ";"
PROPERWORD     = ALPHA  *(ALPHA  /  DIGIT  /  "_" / "-")
WORD           = *(ALPHA  /  DIGIT  /  "_"  /  "-")
SEPARATOR      = "."
COLON          = ":"
VALUE          = *VCHAR

COLUMN-LABEL   = ( "k" / "r" / "i0" / "chi" /
	           "itrans"    / "ifluor"    / "irefer"    / 
		   "mutrans"   / "mufluor"   / "murefer"   / 
		   "normtrans" / "normfluor" / "normrefer" / 
		   "chi_mag"   / "chi_pha"   / "chi_re"    / "chi_im" /
		   "chir_mag"  / "chir_pha"  / "chir_re"   / "chir_im" )

FIELD-END      = COMM  2*"/"  EOL
HEADER-END     = COMM  2*"-"  EOL

;; Version Subsection
XDI-VERSION    = "XDI/"  *DIGIT  ". " *DIGIT
APPLICATIONS   = VALUE
VERSION        = COMM  XDI-VERSION  *APPLICATIONS  EOL

;; Defined fields with specified content
ENERGY         = COMM  "Column"   SEPARATOR INTEGER     COLON "energy" ("ev" / "kev" / "pixel")
ANGLE          = COMM  "Column"   SEPARATOR INTEGER     COLON "angle"  ("degrees" / "radians" / "steps")
OTHER-COLUMN   = COMM  "Column"   SEPARATOR INTEGER     COLON COLUMN-LABEL
STARTTIME      = COMM  "Time"     SEPARATOR "start"     COLON DATETIME
ENDTIME        = COMM  "Time"     SEPARATOR "end"       COLON DATETIME
DSPACING       = COMM  "Beamline" SEPARATOR "d-spacing" COLON FLOAT

;; Generic fields
FIELD-NAME     = PROPERWORD *(SEPARATOR WORD)
FIELD-LINE     = COMM  FIELD-NAME  COLON  *VALUE

FIELDS         = *(  ( ENERGY / ANGLE / OTHER_COLUMN / STARTTIME / ENDTIME / DSPACING / FIELD-LINE ) EOL )

;; Fields subsection
FIELD-SUBSECTION  = FIELDS FIELD-END

;; User comments subsection
COMMENT-LINE   = COMM  *VALUE  EOL
COMMENTS       = *COMMENT-LINE  HEADER-END

;; Column Labels
LABELS         = COMM *WORD  EOL

;; Data Section
DATA-LINE      = *FLOAT EOL
DATA           = *DATA-LINE
